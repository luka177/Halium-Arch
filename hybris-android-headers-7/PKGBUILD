# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Contributor Michael Serpieri <mickybart@pygoscelis.org>

pkgname=(hybris-android-headers-7 hybris-android-headers-7-caf)
_pkgname=android-headers
pkgver=7.1.1
pkgrel=2
pkgdesc='Header files needed to write applications for the Android platform'
url='https://github.com/vanyasem/android-headers'
arch=(x86_64 i686 armv7h aarch64)
license=(custom)
provides=(hybris-android-headers)
conflicts=(hybris-android-headers)
source=("${_pkgname}-7::git+https://github.com/vanyasem/android-headers.git")
sha256sums=('SKIP')
options=('!strip')

package_hybris-android-headers-7() {
  cd ${_pkgname}-7
  install -d "${pkgdir}/opt/android"

  make DESTDIR="${pkgdir}/" PREFIX="/opt/android" INCLUDEDIR="/opt/android/include" PKGCONFIGDIR="/usr/lib/pkgconfig" install
  sed -i 's|Cflags:.*|Cflags: -I${includedir}|' "${pkgdir}/usr/lib/pkgconfig/android-headers.pc"
}

package_hybris-android-headers-7-caf() {
  cd ${_pkgname}-7
  install -d "${pkgdir}/opt/android"

  sed '/\/\* CONFIG GOES HERE/,$d' android-config.h > android-config.h.new
  cat <<EOF >> android-config.h.new
#define QCOM_BSP 1
#define QTI_BSP 1
EOF
  sed '0,/\/\* CONFIG GOES HERE/d' android-config.h >> android-config.h.new
  mv android-config.h.new android-config.h

  make DESTDIR="${pkgdir}/" PREFIX="/opt/android" INCLUDEDIR="/opt/android/include" PKGCONFIGDIR="/usr/lib/pkgconfig" install
  sed -i 's|Cflags:.*|Cflags: -I${includedir}|' "${pkgdir}/usr/lib/pkgconfig/android-headers.pc"
}
# vim:set ts=2 sw=2 et:
