# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=glamor-hybris-git
_pkgname=glamor-hybris
pkgver=610f18a
pkgrel=1
pkgdesc='Glamor Xserver 2D acceleration, modified to work with libhybris drivers'
url='https://github.com/NotKit/glamor-hybris'
arch=(i686 x86_64 armv7h aarch64)
license=()
depends=(xorg-server xproto fontsproto randrproto renderproto)
makedepends=(xorg-server-devel git)
groups=(xorg-drivers xorg)
source=('git+https://github.com/gemian/glamor-hybris')
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always
}

prepare() {
  cd $_pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname
  export CPLUS_INCLUDE_PATH=/opt/android/include:/opt/android/hybris/include
  export C_INCLUDE_PATH=/opt/android/include:/opt/android/hybris/include
  ./configure --prefix=/usr
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
